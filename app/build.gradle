apply plugin: "com.android.application"
apply plugin: "io.fabric"

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "cn.nekocode.hubs"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        def SCHEME = "hubs"
        def HUB_EXTENSION = "hub"
        def HUB_CONFIG_PATH = "config.lua"
        def HUB_USER_CONFIG_PATH = "user_config.lua"

        buildConfigField "String", "SCHEME", "\"$SCHEME\""
        buildConfigField "String", "HUB_EXTENSION", "\"$HUB_EXTENSION\""
        buildConfigField "String", "HUB_CONFIG_PATH", "\"$HUB_CONFIG_PATH\""
        buildConfigField "String", "HUB_USER_CONFIG_PATH", "\"$HUB_USER_CONFIG_PATH\""

        manifestPlaceholders = [
                APPLICATION_ID: applicationId,
                SCHEME: SCHEME,
                HUB_EXTENSION: HUB_EXTENSION,
                FABRIC_API_KEY: "ef319bcb87dd50b6283644f4e9e4c40ab0b07996",
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled true
    }
}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")
    implementation project(":lib-luaj")

    // Android support libraries
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBS_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBS_VERSION"
    implementation "com.android.support:support-annotations:$SUPPORT_LIBS_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBS_VERSION"
    implementation "com.android.support.constraint:constraint-layout:1.1.2"

    // ReactiveX
    implementation "io.reactivex.rxjava2:rxjava:2.1.8"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    def AUTO_DISPOSE_VERSION = "0.8.0"
    implementation "com.uber.autodispose:autodispose:$AUTO_DISPOSE_VERSION"
    implementation "com.uber.autodispose:autodispose-android:$AUTO_DISPOSE_VERSION"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:$AUTO_DISPOSE_VERSION"

    // Network
    implementation "com.squareup.okhttp3:okhttp:3.10.0"
    implementation "com.squareup.picasso:picasso:2.5.2"

    // Others
    implementation "org.jsoup:jsoup:1.11.2"
    implementation "com.github.nekocode:Meepo:0.3"
    implementation "com.evernote:android-state:1.3.1"
    annotationProcessor "com.evernote:android-state-processor:1.3.1"

    // Crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:2.6.8@aar") {
        transitive = true
    }
}
